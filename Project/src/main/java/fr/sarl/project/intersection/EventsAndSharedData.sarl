package fr.sarl.project.intersection

import java.util.UUID
import org.arakhne.afc.math.geometry.d2.d.Vector2d
import org.eclipse.xtend.lib.annotations.Accessors
import java.util.concurrent.ConcurrentHashMap
import java.util.ArrayList
import java.util.Map
import java.util.HashMap

event Die

event Wait

event Go

event Action {
	var influence : Vector2d
}

event JoinIntersection {
	// Id of the car joining the intersection
	var carId : UUID
	
	// Position of the car
	var position : Vector2d
	var roadSection : int
	
	// Targeted road section
	var target : int
	
	new(carId : UUID, position : Vector2d, roadSection : int, target : int) {
		this.carId = carId
		this.position = position
		this.roadSection = roadSection
		this.target = target
	}
}

/** 
 * Event specifying when the GUI must be refreshed according to the new environmental state embodied by the specified map
 * @author Victor Sonza
 */
event GuiRepaint {
	val perceivedAgentBody : ConcurrentHashMap<UUID, IntersectionBody>

	new (bodies : ConcurrentHashMap<UUID, IntersectionBody>) {
		perceivedAgentBody = new ConcurrentHashMap<UUID, IntersectionBody>(bodies);
	}

}

event Start {
	val perceivedAgentBody : ConcurrentHashMap<UUID, CarBody>

	new (bodies : ConcurrentHashMap<UUID, CarBody>) {
		perceivedAgentBody = new ConcurrentHashMap<UUID, CarBody>(bodies);
	}

}



event LeaveIntersection {
	// Id of the car leaving the intersection
	var carId : UUID
	
	new(car : UUID) {
		carId = car
	}
}

class CarBody {

	// Position of the body
	@Accessors
	var position : Vector2d
	
	// Id of the current road section
	@Accessors
	var roadSection : int // Peut être modifier ça, de manière à ce que la car demande à l'environnement sa road section ???

	// Address of the body's owner
	@Accessors
	var owner : UUID

	// Agent's speed
	@Accessors
	var speed : Vector2d

	// Acceleration
	@Accessors
	var acceleration : Vector2d
	
	new(owner : UUID, position : Vector2d, roadSection : int, speed : Vector2d) {
		this.position = position
		this.roadSection = roadSection
		this.owner = owner
		this.speed = speed
		acceleration = new Vector2d
	}
	
}

class IntersectionBody
{
	@Accessors
	var owner : UUID
	
	// List of the road sections
	@Accessors
	var roadSections : ArrayList<RoadSection>
	
	@Accessors
	var intersectionCenter : IntersectionCenter
	
	new(o: UUID)
	{
		this.owner = o // new(numS : int,posNW : Vector2d, posSE: Vector2d,w : int,l:int, c: ArrayList<CarBody>)
		var w = 100
		var l = 400

		var r1 = new RoadSection(1, new Vector2d(400, 0), new Vector2d(500, 400), w, l, new ArrayList<CarBody>())
		var r2 = new RoadSection(2, new Vector2d(0, 500), new Vector2d(400, 600), w, l, new ArrayList<CarBody>())
		var r3 = new RoadSection(3, new Vector2d(600, 400), new Vector2d(1000, 500), w, l, new ArrayList<CarBody>())
		var r4 = new RoadSection(4, new Vector2d(500, 600), new Vector2d(600, 1000), w, l, new ArrayList<CarBody>())

		roadSections = newArrayList
		roadSections.add(r1)
		roadSections.add(r2)
		roadSections.add(r3)
		roadSections.add(r4)
	}
	
	
	def getCars() :HashMap<UUID, CarBody>
	{
		var res : HashMap<UUID, CarBody> = new HashMap<UUID, CarBody>()
		for(r : roadSections)
		{
			for (cb : r.waitingList)
			{
				res.put(cb.owner,cb)
			}
		}
		
		return res
	}
	
}
